# 인프라 리소스 확장 및 연결 구성 가이드

Terraform을 활용해 OpenStack 인프라를 확장하여 외부에서 접근 가능한 VM 인스턴스를 자동으로 구성

## 목표

Terraform을 활용해 OpenStack 인프라를 확장하여, 외부에서 접근 가능한 VM 인스턴스를 자동으로 구성

## 주요 Terraform 코드

### main.tf 전체 구성

```hcl
resource "openstack_compute_instance_v2" "vm_1" {
  name        = var.instance_name
  image_name  = var.image_name
  flavor_name = var.flavor_name
  
  network {
    name = var.network_name
  }
  
  security_groups = [openstack_networking_secgroup_v2.secgroup_ssh.name]
}

# 보안 그룹 생성
resource "openstack_networking_secgroup_v2" "secgroup_ssh" {
  name = "allow_ssh_icmp"
}

# SSH 포트 허용 규칙
resource "openstack_networking_secgroup_rule_v2" "secgroup_rule_ssh" {
  direction         = "ingress"
  ethertype         = "IPv4"
  protocol          = "tcp"
  port_range_min    = 22
  port_range_max    = 22
  remote_ip_prefix  = "0.0.0.0/0"
  security_group_id = openstack_networking_secgroup_v2.secgroup_ssh.id
}

# ICMP(ping) 허용 규칙
resource "openstack_networking_secgroup_rule_v2" "secgroup_rule_icmp" {
  direction         = "ingress"
  ethertype         = "IPv4"
  protocol          = "icmp"
  remote_ip_prefix  = "0.0.0.0/0"
  security_group_id = openstack_networking_secgroup_v2.secgroup_ssh.id
}

# Floating IP 생성
resource "openstack_networking_floatingip_v2" "fip_1" {
  pool = "public"
}

# Floating IP 인스턴스에 연결
resource "openstack_compute_floatingip_associate_v2" "fip_assoc_1" {
  floating_ip = openstack_networking_floatingip_v2.fip_1.address
  instance_id = openstack_compute_instance_v2.vm_1.id
}
```

### 출력 (outputs.tf)

```hcl
output "instance_ip" {
  value = openstack_compute_instance_v2.vm_1.access_ip_v4
}

output "floating_ip" {
  description = "VM의 Floating IP 주소"
  value       = openstack_networking_floatingip_v2.fip_1.address
}
```

## 테스트 방법

### 인프라 적용 및 IP 확인

```bash
terraform apply
terraform output floating_ip
```

### SSH 접속 테스트

```bash
ssh stack@<Floating IP>
```

보안 그룹과 Floating IP가 정상적으로 적용되었는지 SSH 접속 테스트

## 검증 포인트

- DevStack 대시보드(`https://a.b.c.d/dashboard`)에서 생성된 리소스 확인
- 보안 그룹 규칙이 올바르게 적용되었는지 확인
- Floating IP가 인스턴스에 정상 할당되었는지 확인
- 외부에서 SSH 접속이 정상 작동하는지 확인

---
