# 인프라 리소스 확장 및 연결 구성 가이드

Terraform을 활용해 OpenStack 인프라를 확장하여 외부에서 접근 가능한 VM 인스턴스를 자동으로 구성

## 사전 준비

### 기존 인스턴스 삭제

이전 단계에서 생성한 인스턴스가 있다면 먼저 삭제:

```bash
terraform destroy -auto-approve
```

## 목표

Terraform을 활용해 OpenStack 인프라를 확장하여, 외부에서 접근 가능한 VM 인스턴스를 자동으로 구성

## 주요 Terraform 코드

### main.tf 전체 구성

```hcl
resource "openstack_compute_instance_v2" "vm_1" {
  name        = var.instance_name
  image_name  = var.image_name
  flavor_name = var.flavor_name
  key_pair    = var.key_name
  
  network {
    name = var.network_name
  }
  
  security_groups = [openstack_networking_secgroup_v2.secgroup_ssh.name]
}

# 보안 그룹 생성
resource "openstack_networking_secgroup_v2" "secgroup_ssh" {
  name = "allow_ssh_icmp"
}

# SSH 포트 허용 규칙
resource "openstack_networking_secgroup_rule_v2" "secgroup_rule_ssh" {
  direction         = "ingress"
  ethertype         = "IPv4"
  protocol          = "tcp"
  port_range_min    = 22
  port_range_max    = 22
  remote_ip_prefix  = "0.0.0.0/0"
  security_group_id = openstack_networking_secgroup_v2.secgroup_ssh.id
}

# ICMP(ping) 허용 규칙
resource "openstack_networking_secgroup_rule_v2" "secgroup_rule_icmp" {
  direction         = "ingress"
  ethertype         = "IPv4"
  protocol          = "icmp"
  remote_ip_prefix  = "0.0.0.0/0"
  security_group_id = openstack_networking_secgroup_v2.secgroup_ssh.id
}

# HTTP 포트 허용 규칙 (웹서버용)
resource "openstack_networking_secgroup_rule_v2" "secgroup_rule_http" {
  direction         = "ingress"
  ethertype         = "IPv4"
  protocol          = "tcp"
  port_range_min    = 80
  port_range_max    = 80
  remote_ip_prefix  = "0.0.0.0/0"
  security_group_id = openstack_networking_secgroup_v2.secgroup_ssh.id
}

# Floating IP 생성
resource "openstack_networking_floatingip_v2" "fip_1" {
  pool = "public"
}

# Floating IP 인스턴스에 연결
resource "openstack_compute_floatingip_associate_v2" "fip_assoc_1" {
  floating_ip = openstack_networking_floatingip_v2.fip_1.address
  instance_id = openstack_compute_instance_v2.vm_1.id
}
```

### 출력 (outputs.tf)

```hcl
output "instance_ip" {
  value = openstack_compute_instance_v2.vm_1.access_ip_v4
}

output "floating_ip" {
  description = "VM의 Floating IP 주소"
  value       = openstack_networking_floatingip_v2.fip_1.address
}
```

## Ubuntu Cloud Image로 전환

### 1. SSH 키 페어 생성

`.ssh` 디렉토리를 먼저 생성:

```bash
mkdir -p ~/.ssh
```

키페어 생성:

```bash
openstack keypair create mykey > ~/.ssh/mykey.pem
chmod 600 ~/.ssh/mykey.pem
```

정상적으로 생성되었는지 확인:

```bash
ls -l ~/.ssh/mykey.pem
openstack keypair list
```

### 2. Ubuntu 이미지 다운로드

OpenStack에서 사용할 qcow2 이미지 다운로드:

```bash
wget https://cloud-images.ubuntu.com/jammy/current/jammy-server-cloudimg-amd64.img
```

### 3. 이미지 커스터마이징 (root 로그인용)

Ubuntu 이미지에 root 비밀번호 설정 및 SSH 설정:

```bash
# root 비밀번호 설정 + SSH 설정 + cloud-init 비활성화
sudo virt-customize -a jammy-server-cloudimg-amd64.img \
  --root-password password:1234 \
  --run-command 'passwd -u root' \
  --run-command 'systemctl enable ssh' \
  --run-command 'sed -i "s/^#\?PermitRootLogin.*/PermitRootLogin yes/" /etc/ssh/sshd_config' \
  --run-command 'sed -i "s/^#\?PasswordAuthentication.*/PasswordAuthentication yes/" /etc/ssh/sshd_config' \
  --run-command 'touch /etc/cloud/cloud-init.disabled'
```

### 4. Glance에 등록

```bash
openstack image create "ubuntu-22.04" \
  --file jammy-server-cloudimg-amd64.img \
  --disk-format qcow2 --container-format bare \
  --public
```

**기존 이미지가 있는 경우**:
```bash
openstack image delete ubuntu-22.04
```

### 5. Terraform 변수 변경

`variables.tf`에서 이미지와 flavor 변경:

```hcl
variable "image_name" {
  description = "사용할 이미지 이름"
  default     = "ubuntu-22.04"
}

variable "flavor_name" {
  description = "인스턴스에 사용할 flavor"
  default     = "m1.medium"
}

variable "key_name" {
  description = "SSH 접속에 사용할 key pair 이름"
  default     = "mykey"
}
```

## 테스트 방법

### 인프라 적용 및 IP 확인

```bash
terraform apply
terraform output floating_ip
```

### SSH 접속 방법

1. **Host key 충돌 해결** (이전에 같은 IP 사용한 경우):
   ```bash
   ssh-keygen -f "/home/stack/.ssh/known_hosts" -R "<floating_ip>"
   ```

2. **SSH 키를 이용한 접속**:
   ```bash
   ssh -i ~/.ssh/mykey.pem ubuntu@<floating_ip>
   ```

**참고**: 본인은 호스트PC에서 OpenStack VM SSH 접속이 되지 않아 게스트 VM(Ubuntu)를 이용해 SSH 접속하였음

### 웹서버(Nginx) 설치

SSH 접속 후 Nginx 웹서버 설치:

```bash
# 패키지 업데이트
sudo apt update
sudo apt upgrade -y

# Nginx 설치
sudo apt install -y nginx

# Nginx 서비스 시작 및 활성화
sudo systemctl start nginx
sudo systemctl enable nginx

# 상태 확인
sudo systemctl status nginx
```

웹 브라우저에서 `http://<floating_ip>`로 접속하여 Nginx 기본 페이지 확인

## 검증 포인트

- DevStack 대시보드(`https://a.b.c.d/dashboard`)에서 생성된 리소스 확인
- 보안 그룹 규칙이 올바르게 적용되었는지 확인
- Floating IP가 인스턴스에 정상 할당되었는지 확인
- 외부에서 SSH 접속이 정상 작동하는지 확인

---

**참고**: 다음 단계에서는 이 구성을 바탕으로 더 복잡한 네트워크 구성 및 멀티 인스턴스 환경을 다룰 예정
